#!/usr/bin/env bb
;; vim: set ft=clojure

"Script to take screenshot and save them under ~/screenshots.
Can also upload them directly to my vrac server"

(require '[babashka.process :refer [shell] :as p]
         '[babashka.cli :as cli]
         '[babashka.fs :as fs])

(def cli-options
  {:coerce {:hours :int}
   :alias {:u :upload
           :n :no-expires}
   :exec-args {:hours 48}})

(defn shot-and-upload [{tok-name :upload hours :hours test :test}]
  (let [tmp-path (str "/tmp/" tok-name ".png")]
    (shell "scrot" "--select" tmp-path)
    (try
      (let [args ["vrac" "upload"
                  "--expires-hours" (str hours)
                  "--name" tok-name
                  (when test
                    ["--base-url" "http://localhost:8000"])
                  tmp-path]
            result
            (apply shell {:out :string :err :string} (filter some? (flatten args)))]
        (.print *err* (:err result))
        (->> result :out (shell "xdg-open")))
      (catch clojure.lang.ExceptionInfo exc
        (shell "/usr/bin/notify-send" "--urgency" "critical" (:err (ex-data exc))))
      (finally
        (fs/delete tmp-path)))))

(let [opts (cli/parse-opts *command-line-args* cli-options)]
  (if (nil? (:upload opts))
    (shell
     "scrot"
     "--select"
     (str (System/getenv "HOME")
          "/screenshots/%Y-%m-%d-$wx$h-scrot.png"))
    (shot-and-upload opts)))
